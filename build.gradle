import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.gradleup.shadow' version '9.1.0'
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

allprojects {
    apply plugin: "java"
    apply plugin: 'java-library'
    apply plugin: "com.gradleup.shadow"

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    group 'org.vivecraft'
    version = plugin_version

    configurations.configureEach {
        exclude group: "net.md-5", module: "bungeecord-chat"
    }

    repositories {
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        // elkmakers maven, for the mapping files
        maven {
            url = "https://maven.elmakers.com/repository/"
            content {
                includeGroup 'org.spigotmc'
            }
        }

        maven {
            name = 'minecraft'
            url = 'https://libraries.minecraft.net'
            content {
                includeModule("com.mojang", "brigadier")
            }
        }

        // add the ScreamingSandals repository for the mapping bundle
        maven {
            url = "https://repo.screamingsandals.org/public"
            content {
                includeGroup("me.kcra.takenaka")
            }
        }

        maven {
            url = 'https://jitpack.io'
        }

        maven {
            url = "https://maven.apache.org"
        }

        mavenCentral()
    }

    javadoc {
        if (project.name != "core") {
            enabled = false
        }
        destinationDir = file("../build/javadoc")
        include 'org/vivecraft/api/**'
        options.windowTitle("Vivecraft API")
        title = "Vivecraft Spigot API"
        options.locale("en_US")
        // also force en_us for the java runtime, because of a java bug
        options.jFlags("-Duser.country=US", "-Duser.language=en")
    }

    dependencies {
        compileOnly('org.jetbrains:annotations:26.0.2')
        implementation('org.joml:joml:1.10.8') { transitive = false }

        // mc libs
        compileOnly("io.netty:netty-handler:4.1.127.Final")
        compileOnly("com.google.code.gson:gson:2.2.4")

        // the small library needed for the accessors to function
        implementation("me.kcra.takenaka:generator-accessor-runtime:$takenaka_version")
    }
}

dependencies {
    subprojects.each { subproj ->
        if (subproj.name != "generate" && subproj.name != "stubs") {
            if (subproj.getTasksByName("remap", false).isEmpty()) {
                implementation subproj
            } else {
                implementation project(path: subproj.path, configuration: "remapped")
            }
        }
    }
}

// make sure remap is ran first
gradle.projectsEvaluated {
    tasks.matching { it.name == 'shadowJar' }.configureEach { ShadowJar shadow ->
        subprojects.each { p ->
            if (p.tasks.findByName('remap')) shadow.dependsOn(p.tasks.named('remap'))
        }
    }
}

shadowJar {
    archiveClassifier.set('')
    //relocate 'org.joml', 'shadow.io.org.joml'
    // exclude the minecraft files that slip in there from the remapping process
    exclude "net/**"
    /*minimize() {
        subprojects.each { subproj ->
            exclude dependency(subproj)
        }
    }*/
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

tasks.register('printProjectName') {
    doLast {
        println rootProject.name
    }
}

tasks.register('release') {
    dependsOn build
    dependsOn ":core:apiJar"
    dependsOn ":core:apiSourcesJar"
}

publishing {
    repositories {
        maven {
            name = "github-packages"
            url = uri("https://maven.pkg.github.com/vivecraft/vivecraft-spigot-extension")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        api(MavenPublication) {
            groupId = 'org.vivecraft'
            artifactId = 'vivecraft-spigot-extension-api'
            version = api_version

            artifact(project(':core').tasks.named('apiJar')){
                classifier = null
            }
            artifact(project(':core').tasks.named('apiSourcesJar')){
                classifier = 'sources'
            }
        }
    }
}
